# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":devicetree-test-package" ]
}

source_set("devicetree") {
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_cpp",
    "//sdk/lib/driver/legacy-bind-constants",
    "//sdk/lib/driver2:cpp",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//zircon/kernel/lib/devicetree",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxc",
  ]

  sources = [
    "manager.cc",
    "node.cc",
  ]
}

test("devicetree-test") {
  deps = [
    ":devicetree",
    "//sdk/lib/driver_runtime/testing/loop_fixture:gtest",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//src/lib/fxl/test:gtest_main",
  ]

  sources = [ "manager-test.cc" ]
}

resource("simple-dtb") {
  sources = [ "test-data/simple.dtb" ]
  outputs = [ "test-data/{{source_file_part}}" ]
}

fuchsia_unittest_component("devicetree-test-component") {
  component_name = "devicetree-test"
  deps = [
    ":devicetree-test",
    ":simple-dtb",
  ]
}

fuchsia_test_package("devicetree-test-package") {
  test_components = [ ":devicetree-test-component" ]
  package_name = "devicetree-test"
}
