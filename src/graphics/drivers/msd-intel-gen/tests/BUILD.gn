# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("tests") {
  testonly = true
  deps = [
    ":integration",
    ":nonhardware_tests",
  ]
}

group("nonhardware_tests") {
  testonly = true
  deps = [ ":msd_intel_gen_nonhardware_tests" ]
}

group("integration") {
  testonly = true
  deps = [
    ":magma-conformance-tests-intel",
    ":msd_intel_gen_integration_tests",
  ]
}

fuchsia_test_component("msd_intel_gen_nonhardware_tests_component") {
  deps = [ "unit_tests:msd_intel_gen_nonhardware_tests" ]
  manifest = "meta/msd_intel_gen_nonhardware_tests.cml"
}

fuchsia_test_package("msd_intel_gen_nonhardware_tests") {
  test_components = [ ":msd_intel_gen_nonhardware_tests_component" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_test_component("msd_intel_gen_integration_tests_component") {
  deps = [ "integration:msd_intel_gen_integration_tests" ]
  manifest = "meta/msd_intel_gen_integration_tests.cml"
  test_type = "device"
}

fuchsia_test_package("msd_intel_gen_integration_tests") {
  test_components = [ ":msd_intel_gen_integration_tests_component" ]
  test_specs = {
    environments = magma_intel_gpu_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_component("vendor-helper-intel-component") {
  testonly = true
  component_name = "vendor-helper"
  manifest = "meta/vendor-helper.cml"
  deps = [ "integration:magma-conformance-vendor-helper-intel" ]
}

fuchsia_test_component("magma-conformance-tests-intel-component") {
  component_name = "magma-conformance-tests"
  manifest = "meta/magma-conformance-tests-intel.cml"
  test_type = "system"
}

fuchsia_test_package("magma-conformance-tests-intel") {
  test_components = [ ":magma-conformance-tests-intel-component" ]
  subpackages =
      [ "//src/graphics/magma/tests/integration:magma-conformance-tests-child" ]
  deps = [ ":vendor-helper-intel-component" ]
  test_specs = {
    environments = magma_intel_gpu_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
