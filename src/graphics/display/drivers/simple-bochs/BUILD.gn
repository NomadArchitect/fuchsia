# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("simple-bochs-bind") {
  rules = "meta/simple-bochs.bind"
  tests = "meta/bochs_bind_tests.json"

  deps = [
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_bindlib",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_bindlib",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.pci",
  ]
}

fuchsia_driver("simple.bochs-driver") {
  output_name = "simple.bochs"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]

  sources = [
    "bochs-vbe-registers.h",
    "simple-bochs.cc",
  ]
  deps = [
    ":simple-bochs-bind",
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/graphics/display/lib/simple-display",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
  ]
}

fuchsia_driver_component("simple.bochs") {
  component_name = "simple.bochs"
  deps = [ ":simple.bochs-driver" ]
  info = "meta/simple.bochs-info.json"
  manifest = "meta/bochs.cml"
}

fuchsia_driver_package("simple-bochs-package") {
  package_name = "simple.bochs"
  driver_components = [ ":simple.bochs" ]
}

group("tests") {
  testonly = true
  deps = [ ":simple-bochs-bind_test" ]
}
