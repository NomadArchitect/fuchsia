# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("simple-intel-bind") {
  rules = "meta/simple-intel.bind"
  tests = "meta/intel_bind_tests.json"

  deps = [
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_bindlib",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_bindlib",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.pci",
  ]
}

fuchsia_driver("simple.intel-driver") {
  output_name = "simple.intel"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]

  sources = [ "simple-intel.cc" ]
  deps = [
    ":simple-intel-bind",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/logging/cpp",
    "//sdk/lib/zbi-format",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/graphics/display/lib/simple-display",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver_component("simple.intel") {
  component_name = "simple.intel"
  deps = [ ":simple.intel-driver" ]
  info = "meta/simple.intel-info.json"
  manifest = "meta/intel.cml"
}

fuchsia_driver_package("simple-intel-package") {
  package_name = "simple.intel"
  driver_components = [ ":simple.intel" ]
}

group("tests") {
  testonly = true
  deps = [ ":simple-intel-bind_test" ]
}
