# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/config_data_for_assembly.gni")
import("//build/config.gni")

# The list of Sampler configuration files in the current directory that
# are used to configure data to be sent to Cobalt. Files that are not listed
# in this variable will be ignored.
project_files = [
  "archivist.json",
  "bt_host.json",
  "driver_manager_all.json",
  "fshost.json",
  "netstack.json",
  "netstack3.json",
  "power_metrics_recorder.json",
  "setui.json",
  "system-update-committer.json",

  # TODO(https://fxbug.dev/): remove driver_manager_all.json and bring back these
  #"audio-drivers.json",
  #"connectivity-wlan.json",
  #"display.json",
  #"ftl.json",
  #"fvm.json",
  #"radar.json",
  #"sdmmc.json",
  #"sysmem.json",
]

fire_components_file = [ "fire/components.json5" ]

fire_project_files = [
  "fire/projects/cpu.json5",
  "fire/projects/crashes.json5",
  "fire/projects/escrow.json5",
  "fire/projects/minfs.json5",
]

# Install FIRE config into the "sampler" package

config_data_for_assembly("fire_config_component_list") {
  for_pkg = "sampler"
  sources = fire_components_file
  outputs = [ "fire/fuchsia/{{source_file_part}}" ]
}

config_data_for_assembly("fire_config_projects") {
  for_pkg = "sampler"
  sources = fire_project_files
  outputs = [ "fire/fuchsia/projects/{{source_file_part}}" ]
}

# Install Sampler config into the "sampler" package

config_data_for_assembly("sampler_config_internal") {
  for_pkg = "sampler"
  sources = project_files

  # The component will see this as /config/metrics/fuchsia/foo.json
  outputs = [ "metrics/fuchsia/{{source_file_part}}" ]
}

# Validate Sampler config

resource("sampler-validation-resources") {
  sources = project_files

  # The component will see this as /pkg/config/metrics/fuchsia/foo.json
  outputs = [ "config/metrics/fuchsia/{{source_file_part}}" ]
}

resource("fire-component-config-validation-resources") {
  sources = fire_components_file

  # The component will see this as /pkg/config/fire/fuchsia/components.json5
  outputs = [ "config/fire/fuchsia/{{source_file_part}}" ]
}

resource("fire-project-config-validation-resources") {
  sources = fire_project_files

  # The component will see this as /pkg/config/fire/fuchsia/projects/foo.json5
  outputs = [ "config/fire/fuchsia/projects/{{source_file_part}}" ]
}

# Test component for config validation.
fuchsia_unittest_component("config-validation-component") {
  deps = [
    ":fire-component-config-validation-resources",
    ":fire-project-config-validation-resources",
    ":sampler-validation-resources",
    "//src/diagnostics/sampler/tests:config-validation",
  ]
}

# This target name must be unique in the build tree
fuchsia_test_package("sampler-config-validation") {
  test_components = [ ":config-validation-component" ]
}

group("tests") {
  testonly = true
  deps = [ ":sampler-config-validation" ]
}
